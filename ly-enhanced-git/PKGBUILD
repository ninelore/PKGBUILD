# Maintainer: Ingo Reitz <ninelore@protonmail.com>
pkgname=ly-enhanced-git
pkgver=0.6.0.r0.gb31974c
pkgrel=1
pkgdesc="TUI display manager with silenced Xorg STDOUT"
arch=('i686' 'x86_64' 'aarch64')
url="https://github.com/ninelore/ly-enhanced"
license=('custom:WTFPL')
makedepends=('git')
depends=('pam' 'xorg-xauth')
conflicts=('ly' 'ly-git' 'python-ly-git')
backup=('etc/ly/config.ini')
source=('git+https://github.com/ninelore/ly-enhanced.git')
md5sums=('SKIP')

pkgver() {
	cd ly-enhanced
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd ly-enhanced
	make github
}

build() {
	cd ly-enhanced
	make
}

package() {
	cd ly-enhanced
	make DESTDIR="$pkgdir" install
	install -D -m644 license.md "${pkgdir}/usr/share/licenses/${pkgname}/WTFPL"
}
