# Maintainer: Ingo Reitz <ninelore@protonmail.com>
pkgname=chrultrabook-tools
gitname=Chrultrabook-Tools
pkgver=2.8.3
pkgrel=1
pkgdesc="User-friendly configuration utility for Chromebooks running an alternate OS"
arch=('x86_64')
url="https://github.com/death7654/Chrultrabook-Tools"
license=('GPL-3.0-only')
depends=('webkit2gtk')
makedepends=('nodejs' 'npm' 'cargo' 'base-devel' 'curl' 'wget' 'file' 'openssl' 'appmenu-gtk-module' 'gtk3' 'libappindicator-gtk3' 'librsvg' 'libvips')
source=("$gitname-$pkgver.tar.gz::https://github.com/death7654/Chrultrabook-Tools/archive/refs/tags/$pkgver.tar.gz"
        "https://tree123.org/chrultrabook/utils/ectool"
        "https://tree123.org/chrultrabook/utils/cbmem")
sha256sums=('eb49903ceda73542d096818851a5ec2df1cb223e409b28154ba9faf1495bf7d3'
            '8399b77a95d3b305c11f5283a1dfcd3ac9d6ce92b7731a4746abbec9229ba88c'
            'e8634d54a05593b49c8481caebc8f47446e574517dd478bfba651dcc8a2b8882')

build() {
	cd "$srcdir/$gitname-$pkgver"
	npm install
	npx tauri build -b none
}

package() {
	cd "$srcdir/$gitname-$pkgver"
    
    # binaries
	install -Dm755 target/release/chrultrabook-tools "${pkgdir}/usr/bin/chrultrabook-tools"
    install -Dm755 "${srcdir}/ectool" "${pkgdir}/usr/bin/ectool"
    install -Dm4755 "${srcdir}/cbmem" "${pkgdir}/usr/bin/cbmem"

    # udev rule
    install -Dm755 linux/99-cros-ec.rules "${pkgdir}/etc/udev/rules.d/99-cros-ec.rules"

    # licence
    install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}