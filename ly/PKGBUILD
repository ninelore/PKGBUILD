# Maintainer: Ingo Reitz <ninelore@protonmail.com>
pkgname=ly-quiet-git
pkgver=0.5.1.r0.gf6658f8
pkgrel=1
pkgdesc="TUI display manager with silenced Xorg STDOUT"
arch=('i686' 'x86_64' 'aarch64')
url="https://github.com/ninelore/ly"
license=('custom:WTFPL')
makedepends=('git')
depends=('pam' 'xorg-xauth')
conflicts=('ly' 'python-ly-git')
backup=('etc/ly/config.ini')
source=('git+https://github.com/ninelore/ly.git')
md5sums=('SKIP')

pkgver() {
	cd ly
	git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd ly
	make github
}

build() {
	cd ly
	make
}

package() {
	cd ly
	make DESTDIR="$pkgdir" install
	install -D -m644 license.md "${pkgdir}/usr/share/licenses/${pkgname}/WTFPL"
}
